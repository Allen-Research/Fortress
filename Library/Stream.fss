(*******************************************************************************
    Copyright 2008,2010, Oracle and/or its affiliates.
    All rights reserved.


    Use is subject to license terms.

    This distribution may include materials developed by third parties.

 ******************************************************************************)

component Stream
import FlatString.FlatString
export Stream

(***********************************************************
 * Types to support input and output
 ***********************************************************)

trait Closeable
    close():()
end

trait FileStream extends Closeable
    getter fileName():String
end

trait Consumable
    consume():()
    whenUnconsumed():()
end

trait WriteStream extends { Closeable }

    (** %write(FlatString)% and %write(Char)% are the primitive mechanisms
        for writing characters to  a %WriteStream%. **)

    abstract write(s: String):()
    abstract write(c: Char):()

    (** %write(Any)% converts its argument to a String using %asString%
        and appends the result to the stream. **)
    write(x: Any):() =
            typecase x of
                x':Object ⇒ write x'.asString
                else   ⇒ write ("" || x)                   (* handles tuples *)
            end

    (** %writes% converts each of the generated elements to a string
        using %asString% unless the element is already a %String% in
        which case it is left alone.  The resulting strings are
        appended together to the end of the stream.  To avoid
        interleaving with concurrent output to the same stream, this
        append step is done monolithically. **)
    writes(x:Generator[\Any\]):() = writes(x,"")
    writes(x: Generator[\Any\], rest: String):() =
        write((BIG ||[e ← x]
                   typecase e of
                        e':String ⇒ e'
                        e':Object ⇒ e'.asString
                        else   ⇒ ("" || e)                 (* handles tuples *)
                   end) || rest)

    (** %print% and %println% convert each of their arguments to a
        string using %asString% unless the element is already a
        %String% in which case it is left alone.  The resulting
        strings are appended together monolithically to the end of the
        stream.  The %println% method also appends a newline.  Both
        then flush the stream. *)
    print(args:Any...): () = do writes(args,""); flush() end
    println(args:Any...): () = do writes(args,"\n"); flush() end

    (** %flush% any output to the stream that is still buffered. **)
    abstract flush():()

end WriteStream

end Stream
