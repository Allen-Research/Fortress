(*******************************************************************************
    Copyright 2011, Oracle and/or its affiliates.
    All rights reserved.


    Use is subject to license terms.

    This distribution may include materials developed by third parties.

 ******************************************************************************)

component GenTest4
import CompilerAlgebra.{ ... }
export Executable

(***\
   The type `Container[\T\]` "contains" objects of type `T`, in the sense
   that we can check whether a `T` is a member of a `Container[\T\]` using
   `opr IN` and its ken, or using a `case` expression.  Moreover, we require
   that containers be generators.

   The minimal complete definition of a Container is the `IN` operator.
   (Because every container is a generator, we could have chosen to define
   the `IN` operator as `BIG OR [y <- self] elt=y`, but this is rarely
   the best implementation, so it has been left abstract here.)
 \***)

trait Container[\T extends Equality[\T\]\] extends Generator[\T\]
    (** `IN` indicates whether `self` contains `elt`.  **)
    opr IN(elt: T, self): Boolean
    opr NOTIN(elt: T, self): Boolean = NOT (elt IN self)
    opr NI(self, elt: T): Boolean = (elt IN self)
    opr NOTNI(self, elt: T): Boolean = NOT (self NI elt)
end

(***\
   An object of type Generator[\E\] can "generate values of type E."
   Such generated values are called the //elements/ of the generator.

   Generators are used to express iteration in Fortress.  Every
   generated expression in Fortress (such as `for` loops and comprehensions)
   is desugared into calls to methods of `Generator`, chiefly the `generate`
   method.

   Every generator has a notion of a "natural order" (which by default is
   unspecified), which describes the ordering of reduction operations,
   and also describes the order in which elements are produced by the
   sequential version of the same generator (given by the seq(self)
   method).  The default implementation of seq(self) guarantees that these
   orders will match.

   Note in particular that the natural order of a Generator must be
   consistent; that is, if `a SEQV `b then `a` and `b` must yield equivalent
   elements in the same natural order.  However, note that unless a type
   specifically documents otherwise, no particular element ordering is
   guaranteed, nor is it necessary to guarantee that `a=b` implies that
   generators `a` and `b` have the same natural order.

   Note that there are a number of more complex derived generators that
   are specified further down in this file.  These have the same notions
   of natural order and by default are defined in terms of the
   generate() method.

   The `Generator` trait does not assume that equality `=` is defined for
   the generated type.

   The minimal complete definition of a `Generator` is the `generate` method.
 *)

trait Generator[\E\] excludes { Number, Character }
(*)    getter asString(): String = (BIG ||[i <- self] "," i)[1:]

    (** Returns a generator that generates the same objects as this generator
        but using a natural order that is the reverse of this generator's
        natural order. *)
    getter reverse(): Generator[\E\] = SimpleReversedGenerator[\E\](self)

    (** Method `generate` is the core of the `Generator` trait.  It generates
        elements of type `E` and passes them to the `body` function.
        This generation can occur using any mixture of serial and
        parallel execution desired by the author of the generator; by
        default, uses of a generator must assume every call to the
        body occurs in parallel.

        The results of generation are combined using the reduction
        object R, which specifies a monoidal operation (associative
        and with an identity).  Body results must be combined together
        following the natural order of the generator.  The author of
        the generator is free to use the identity element anywhere
        desired in this computation, and to group reductions in any
        way desired; if no elements are generated, the identity must be
        returned. *)
    generate[\R\](r: Reduction[\R\], body: E->R): R

    (** Transforming generators into new generators *)
    (** `map` returns a new generator that generates the results of
        applying the function `f` to the elements of this generator.
        It is not specified whether any of the applications of `f`
        occur before the new generator is returned.  The resulting
        generator must have the same ordering and cross product
        properties as the generator from which it is derived. *)
    map[\Gyy\](f: E->Gyy): Generator[\Gyy\] = SimpleMappedGenerator[\E,Gyy\](self, f)

    (** seq produces a sequential version of the same generator, which
        produces elements in a temporal order that matches this generator's
        natural order. *)
    seq(self): SequentialGenerator[\E\]   (*) = NaiveSequentialGenerator[\E\](self)

    (** Nesting of generators: the argument `f` is a function from
        elements of this generator to new generators.  This allows an
        inner generator expression to be data-dependent on this (outer)
        generator.  The result is a new generator that will produce all
        the elements of all the generators resulting from applying `f` to
        every element of this generator.  Appropriate overloading of this
        method allows the production of specialized implementations
        when particular sorts of generators are nested.  The natural order
        of the new generator is the concatenation of the natural orders of the
        inner generators, where such concatenation is done in the natural order
        of this generator.  So for example, if we write:
          (0#3).nest[\ZZ32\](\(n:ZZ32):Generator[\ZZ32\] => (n*100#4))
        then the twelve elements of the result should appear in the natural
        order as 0, 1, 2, 3, 100, 101, 102, 103, 200, 201, 202, 203.
     **)
    nest[\G\](f: E -> Generator[\G\]): Generator[\G\] =
        SimpleNestedGenerator[\E,G\](self,f)

    (** Filtering data from a generator.  Only elements that satisfy
        the predicate p are retained.  Natural order and cross product
        properties are otherwise preserved. **)
    filter(f: E -> Condition[\()\]): Generator[\E\] =
        SimpleFilterGenerator[\E\](self,f)

    (** Cross product of two generators.  This is specifically
        designed to be overloaded, such that pairs of independent
        generators can be combined to produce a generator which
        possibly interleaves the iteration spaces of the two
        generators.  For example, we might combine `(0#16).cross(0#32)`
        such that it first splits the second range in half, then the
        first range in half, then the second, and so forth.

        Consider a grid for which the rows are labeled from top to
        bottom with the elements of a in natural order, and the
        columns are labeled from left to right with the elements of g
        in natural order.  Each point in the grid corresponds to a
        pair (a,b) that must be generated by self.cross(g).  In the
        natural order of the cross product, an element must occur
        after those that lie above and to the left of it in the grid.
        By default the natural order of the cross product is
        left-to-right, top to bottom.  Programmers must not rely on
        the default order, except that cross products involving one or
        more sequential generators are always performed in the default
        order.  Note that this means that the following have the same
        natural order:
           seq(a).cross(b)
           a.cross(seq(b))
           seq(a).cross(seq(b))
        But seq(a.cross(b)) may have a different natural order. *)
    cross[\G\](g: Generator[\G\]): Generator[\(E,G)\] =
        SimplePairGenerator[\E,G\](self, g)

    (** Derived generation operations *)
    (** mapReduce is equivalent to generate, but takes an explicit `join` function
        and an identity value `id` that can have any type.  It still assumes `join` is
        associative and that `id` is the identity of `join`. *)
    mapReduce[\R\](body: E->R, join: (R,R)->R, id: R): R =
        generate[\R\](MapReduceReduction[\R\](join, id), body)

    (** `reduce` is like `generate` or `mapReduce` but takes no `body` function *)
    reduce(r: Reduction[\E\]): E = generate[\E\](r, fn(e:E)=>e)
    reduce(join: (E,E)->E, id: E): E = mapReduce[\E\](fn (e:E)=>e, join, id)

    (** `loop` is a version of `generate` that requires a body of void type.
        It is used to translate `for` loops.  This default implementation uses
        `VoidReduction` to reduce many void values to one, thus synchronizing
        completion of the `for` loop. *)
    loop(body :E->()): () = generate[\()\](VoidReduction, body)

end Generator


trait SequentialGenerator[\E\] extends { Generator[\E\] }
    getter reverse(): SequentialGenerator[\E\] =
        SimpleSequentialReversedGenerator[\E\](self)
    seq(self): SequentialGenerator[\E\] = self
    map[\G\](f: E->G): SequentialGenerator[\G\] =
        SimpleSequentialMappedGenerator[\E,G\](self,f)
    (* This is an overloading, so that nesting two sequential
       generators will produce a sequential generator. *)
    nest[\G\](f: E -> SequentialGenerator[\G\]) =
        SimpleSequentialNestedGenerator[\E,G\](self,f)
    (* This is an overloading, so that crossing two sequential
       generators will produce a sequential generator. *)
    cross[\F1\](g: SequentialGenerator[\F1\]): Generator[\(E,F1)\] =
        SimpleSequentialPairGenerator[\E,F1\](self,g)
end SequentialGenerator


(* Reversed generators *)

trait ReversedGenerator[\E\] extends Generator[\E\]
    getter g():Generator[\E\]
    getter reverse(): Generator[\E\] = self.g
    generate[\R\](r:Reduction[\R\], body:E->R): R = self.g.generate[\R\](r.reverse, body)
    map[\G\](f: E->G): Generator[\G\] = self.g.map[\G\](f).reverse
    filter(f: E -> Condition[\()\]): Generator[\E\] = self.g.filter(f).reverse
end

object SimpleReversedGenerator[\E\](g0:Generator[\E\]) extends ReversedGenerator[\E\]
    getter g(): Generator[\E\] = g0
    getter asString(): String = "SimpleReversedGenerator(" self.g.asString ")"
end

object SimpleSequentialReversedGenerator[\E\](g0:SequentialGenerator[\E\])
        extends { ReversedGenerator[\E\], SequentialGenerator[\E\] }
    getter g(): SequentialGenerator[\E\] = g0
    getter asString(): String = "seq(SimpleReversedGenerator(" self.g.asString "))"
    getter reverse(): SequentialGenerator[\E\] = self.g
    map[\G\](f: E->G): SequentialGenerator[\G\] = self.g.map[\G\](f).reverse
    filter(f: E -> Condition[\()\]): Generator[\E\] = self.g.filter(f).reverse   (*) Could return SequentialGenerator[\E\] ?
end


(* Mapped generators *)

trait MappedGenerator[\E,F2\] extends Generator[\F2\]
    getter g(): Generator[\E\]
    getter f(): E -> F2
    getter reverse(): SimpleMappedGenerator[\E,F2\] =
        SimpleMappedGenerator[\E,F2\](self.g.reverse, self.f)
    generate[\R\](r: Reduction[\R\], m: F2->R): R =
        self.g.generate[\R\](r, fn (x: E): R => m((self.f)(x)))
    reduce(r: Reduction[\F2\]): F2 =
        self.g.generate[\F2\](r, self.f)
    map[\G\](f': F2->G): SimpleMappedGenerator[\E,G\] =
        SimpleMappedGenerator[\E,G\](self.g, fn (x: E): G => f'((self.f)(x)))
    seq(self): SimpleSequentialMappedGenerator[\E,F2\] =
        SimpleSequentialMappedGenerator[\E,F2\](seq(self.g),self.f)
end

object SimpleMappedGenerator[\E,F3\](g0: Generator[\E\], f0: E->F3)
        extends MappedGenerator[\E,F3\]
    getter g(): Generator[\E\] = g0
    getter f(): E->F3 = f0
    getter asString(): String = "mapped(" (self asif Generator[\F3\]).asString ")"
end

object SimpleSequentialMappedGenerator[\E,F4\](g0: SequentialGenerator[\E\], f0: E->F4)
        extends { MappedGenerator[\E,F4\], SequentialGenerator[\F4\] }
    getter g(): SequentialGenerator[\E\] = g0
    getter f(): E->F4 = f0
    getter asString() = "mapped(seq(" self.g.asString "))"
    getter reverse(): SimpleSequentialMappedGenerator[\E,F4\] =
        SimpleSequentialMappedGenerator[\E,F4\](self.g.reverse, self.f)
    seq(self): SimpleSequentialMappedGenerator[\E,F4\] = self
end

(* Nested generators *)


trait NestedGenerator[\E,F5\] extends Generator[\F5\]
    getter g(): Generator[\E\]
    getter f(): E -> Generator[\F5\]
    getter reverse(): NestedGenerator[\E,F5\] =
        SimpleNestedGenerator[\E,F5\](self.g.reverse,
            fn(e:E):Generator[\F5\] => (self.f)(e).reverse)
    generate[\R\](r: Reduction[\R\], body: F5->R): R =
        self.g.generate[\R\](r,fn (e:E):R => (self.f)(e).generate[\R\](r,body))
    map[\G\](h:F5->G): Generator[\G\] =
        self.g.nest[\G\](fn (e:E): Generator[\G\] => (self.f)(e).map[\G\](h))
    nest[\G\](h:F5->Generator[\G\]): Generator[\G\] =
        self.g.nest[\G\](fn (e:E):Generator[\G\] => (self.f)(e).nest[\G\](h))
    mapReduce[\R\](body: F5->R, join:(R,R)->R, id:R): R =
        self.g.mapReduce[\R\](
            fn (e:E): R => (self.f)(e).mapReduce[\R\](body,join,id),
            join, id)
    reduce(r: Reduction[\F5\]): F5 =
        self.g.generate[\F5\](r,fn (e:E):F5 => (self.f)(e).reduce(r))
    reduce(join:(F5,F5)->F5, id:F5):F5 =
        self.g.mapReduce[\F5\](fn (e:E): F5 => (self.f)(e).reduce(join,id), join, id)
    loop(body:F5->()): () =
        self.g.loop(fn (e:E) => (self.f)(e).loop(body))
end

object SimpleNestedGenerator[\E,F6\](g0: Generator[\E\], f0: E->Generator[\F6\])
        extends { NestedGenerator[\E,F6\] }
    getter g(): Generator[\E\] = g0
    getter f(): E->Generator[\F6\] = f0
    getter asString() = "(" self.g ").nest(f)"
    seq(self): SequentialGenerator[\F6\] =
      SimpleSequentialNestedGenerator[\E,F6\](seq(self.g),
          typecase self.f of
            f': (E -> SequentialGenerator[\F6\]) => f'
            else => (fn (e:E): SequentialGenerator[\F6\] => seq((self.f)(e)))
          end)
end

object SimpleSequentialNestedGenerator[\E,F7\]
        (g0: SequentialGenerator[\E\], f0: E->SequentialGenerator[\F7\])
        extends { NestedGenerator[\E,F7\], SequentialGenerator[\F7\] }
    getter g(): SequentialGenerator[\E\] = g0
    getter f(): E->SequentialGenerator[\F7\] = f0
    getter asString() = "seq((" self.g ").nest(f))"
end

(* Cross-product generators *)

trait PairGenerator[\E,F8\] extends Generator[\(E,F8)\]
  comprises { SimplePairGenerator[\E,F8\], SimpleSequentialPairGenerator[\E,F8\] }
    getter e(): Generator[\E\]
    getter f(): Generator[\F8\]
    getter reverse(): PairGenerator[\E,F8\] =
        SimplePairGenerator[\E,F8\](self.e.reverse, self.f.reverse)
    generate[\R\](r: Reduction[\R\], m:(E,F8)->R): R =
        self.e.generate[\R\](r, fn (a: E): R =>
                              self.f.generate[\R\](r, fn (b: F8): R => m(a,b)))
end

object SimplePairGenerator[\E,F9\](e0: Generator[\E\], f0: Generator[\F9\])
        extends PairGenerator[\E,F9\]
    getter e(): Generator[\E\] = e0
    getter f(): Generator[\F9\] = f0
    getter asString() = "(" self.e " CROSS " self.f ")"
    seq(self): SequentialGenerator[\(E,F9)\] = SimpleSequentialPairGenerator[\E,F9\](seq(self.e),seq(self.f))
end

object SimpleSequentialPairGenerator[\E,F10\]
        (e0: SequentialGenerator[\E\], f0: SequentialGenerator[\F10\])
        extends { PairGenerator[\E,F10\], SequentialGenerator[\(E,F10)\] }
    getter e(): SequentialGenerator[\E\] = e0
    getter f(): SequentialGenerator[\F10\] = f0
    getter asString() = "seq(" self.e " CROSS " self.f ")"
    getter reverse(): SimpleSequentialPairGenerator[\E,F10\] =
        SimpleSequentialPairGenerator[\E,F10\](self.e.reverse, self.f.reverse)
end

(* Filters *)

trait FilterGenerator[\E\] extends Generator[\E\]
    getter g(): Generator[\E\]
    getter p(): E -> Condition[\()\]
    getter reverse(): FilterGenerator[\E\] = SimpleFilterGenerator[\E\](self.g.reverse, self.p)
    generate[\R\](r:Reduction[\R\], m: E->R): R =
        self.g.generate[\R\](r, fn(e:E):R => if (self.p)(e).holds then m(e) else r.id end)
    reduce(r: Reduction[\E\]): E =
        self.g.generate[\E\](r, fn(e:E):E => if (self.p)(e).holds then e else r.id end)
    filter(p': E -> Condition[\()\]): SimpleFilterGenerator[\E\] =
        SimpleFilterGenerator[\E\](self.g, fn(e:E): Condition[\()\] => if (self.p)(e).holds then p'(e) else NothingObject[\()\] end)
    seq(self): SimpleSequentialFilterGenerator[\E\] =
        SimpleSequentialFilterGenerator[\E\](seq(self.g),self.p)
end

object SimpleFilterGenerator[\E\](g0:Generator[\E\], p0: E->Condition[\()\])
        extends FilterGenerator[\E\]
    getter g(): Generator[\E\] = g0
    getter p(): E -> Condition[\()\] = p0
    getter asString(): String = self.g.asString ".filter(p)"
end

object SimpleSequentialFilterGenerator[\E\](g0: SequentialGenerator[\E\], p0: E->Condition[\()\])
        extends { FilterGenerator[\E\], SequentialGenerator[\E\] }
    getter g(): SequentialGenerator[\E\] = g0
    getter p(): E -> Condition[\()\] = p0
    getter asString() = "seq(" g0.asString ".filter(p))"
    getter reverse(): SimpleSequentialFilterGenerator[\E\] =
        SimpleSequentialFilterGenerator[\E\](self.g.reverse, self.p)
    seq(self): SimpleSequentialFilterGenerator[\E\] = self
    filter(p': E -> Condition[\()\]): SimpleSequentialFilterGenerator[\E\] =
        SimpleSequentialFilterGenerator[\E\](self.g, fn(e:E):Condition[\()\] => if (self.p)(e).holds then p'(e) else NothingObject[\()\] end)
end


(* Conditions *)

(** A Condition is a Generator that generates 0 or 1 element.
    Conditions can be used as nullary comprehension generators or
    as predicates in an if expression. **)
trait Condition[\E\]
(*)      extends { ZeroIndexed[\E\], SequentialGenerator[\E\] }
      extends { SequentialGenerator[\E\] }
(*)     getter isEmpty(): Boolean = NOT self.holds
(*)     getter nonEmpty(): Boolean = NOT self.isEmpty
     getter holds(): Boolean = cond[\Boolean\](fn (_:E):Boolean => true, fn () => false)
(*)     getter size(): ZZ32 = if self.holds then 1 else 0 end
(*)     getter get(): E throws NotFound = cond[\E\](identity[\E\], fn () => throw NotFound)
(*)     getter bounds(): CompactFullRange[\ZZ32\] = 0 # |self|
(*)     getter indices(): Generator[\ZZ32\] = self.bounds
(*)     getter indexValuePairs(): Condition[\(ZZ32,E)\] =
(*)         cond[\E\](fn (e:E) => Just[\(ZZ32,E)\](0,e), fn () => Nothing[\(ZZ32,E)\])
    getter reverse() : Condition[\E\] = self
(*)     opr |self|: ZZ32 = if self.holds then 1 else 0 end
(*)     opr [i:ZZ32]:E throws NotFound = if i=0 then self.get else throw NotFound end
(*)     opr [r:Range[\ZZ32\]]: Condition[\E\] =
(*)         cond[\E\](fn (e:E): Condition[\E\] => do
(*)                     r1 = (0#1).narrowToRange(r)
(*)                     if r1.isEmpty then Nothing[\E\] else self end
(*)                   end,
(*)                 fn (): Condition[\E\] => do r1 = (0#0).narrowToRange(r); self end)
(*)     getDefault(e:E): E = cond[\E\](identity[\E\], fn () => e)
    cond[\G\](t: E -> G, e: () -> G): G
    generate[\G\](r:Reduction[\G\], body: E -> G): G = cond[\G\](body,fn () => r.id)

    (** For a Condition, these methods run eagerly. **)
    map[\Gxx\](f: E->Gxx): Condition[\Gxx\] =
        cond(fn (e:E) => Just[\Gxx\](f(e)), fn () => NothingObject[\Gxx\])
(*)     ivmap[\G\](f: (ZZ32,E)->G): Condition[\G\] =
(*)         cond[\G\](fn (e:E) => Just[\G\](f(0,e)), fn () => Nothing[\G\])
    nest[\G\](f: E -> Generator[\G\]): Generator[\G\] =
        cond(f, fn () => NothingObject[\G\])
    cross[\G\](g: Generator[\G\]): Generator[\(E,G)\] =
        cond(fn (e:E):Generator[\(E,G)\] => g.map[\(E,G)\](fn (g':G):(E,G) => (e,g')),
               fn () => NothingObject[\(E,G)\])
    mapReduce[\R\](body: E->R, _:(R,R)->R, id:R): R =
        cond(body, fn () => id)
    reduce(_:(E,E)->E, id:E):E = cond[\E\](identity[\E\], fn ():E => id)
    reduce(r: Reduction[\E\]):E = cond[\E\](identity[\E\], fn ():E => r.id)
    loop(f:E->()): () = cond[\()\](f,fn ():() => ())
(*)     opr IN(x:E, self):Boolean = cond[\Boolean\](fn (e:E):Boolean => x=e, fn () => false)
end Condition


(* Maybe type *)

value trait Maybe[\E\] extends { Condition[\E\] }
        comprises { NothingObject[\E\], Just[\E\] }
(*)    opr SQCAP(self, other: Maybe[\E\]): Maybe[\E\] = other
  coerce(_: Nothing) = NothingObject[\E\]
end

value object Just[\E\](x: E) extends Maybe[\E\]
    getter asString(): String = "Just(" "XXX" ")"
(*)     getter size(): ZZ32 = 1
(*)     getter get() = x
(*)     getter holds() = true
(*)     opr |self| : ZZ32 = 1
(*)     getDefault(_: E): E = x
(*)     opr IN(y:E, self): Boolean = (y = x)
(*)     opr =(self,other:Just[\E\]): Boolean = (x = other.x)
(*)     opr SQCAP(self, other:Just[\E\]): Maybe[\E\] = if other.x = x then self else NothingObject[\E\] end
    cond[\G\](t: E -> G, e: () -> G): G = t(x)
end

value object NothingObject[\E\] extends Maybe[\E\]
    getter asString():String = "Nothing"
(*)     getter size(): ZZ32 = 0
(*)     getter get() = throw NotFound
(*)     getter holds() = false
(*)     opr |self| : ZZ32 = 0
(*)     getDefault(defaultValue: E):E = defaultValue
(*)     opr IN(x:E, self): Boolean = false
(*)     opr SQCAP(self, other: Maybe[\E\]): NothingObject[\E\] = self
    cond[\G\](t: E -> G, e: () -> G): G = e()
end

value object Nothing end


(************************************************************
* Reductions
************************************************************)

(* This is the trait that the `generate` method of a `Generator` needs to accept.
   In order to encourage parallelism, we require all reductions to be associative.
   In order to make programming the common case of generators easier, we require
   all reductions to have an identity; in situations where the natural join operation
   does not have an identity, trait `NonemptyReduction` can be used to lift that
   operation to a new one that does have an identity. *)

trait Reduction[\R\]
    getter reverse(): Reduction[\R\] = ReversedReduction[\R\](self)
    getter id(): R
    join(a: R, b: R): R
(*)     property FORALL(a: R) join(a, self.id) = a
(*)     property FORALL(a: R) join(self.id, a) = a
(*)     property FORALL(a: R, b: R, c: R) join(join(a,b), c) = join(a,join(b,c))
end

object ReversedReduction[\R\](r: Reduction[\R\]) extends Reduction[\R\]
  getter asString(): String = "ReversedReduction(" r.asString ")"
  getter reverse(): Reduction[\R\] = r
  getter id(): R = r.id
  join(x:R, y:R): R = r.join(y, x)
end

(* Reduction that projects the "body type" `B` onto type `R`,
   reduces within type `R`, then projects final result onto type `F21`.
   This is the trait that the overall implementation of a big operator
   or a comprehension has to deal with. *)

trait GeneralReduction[\B,R,F21\] extends Reduction[\R\]
    getter reverse(): GeneralReduction[\B,R,F21\] = ReversedGeneralReduction[\B,R,F21\](self)
    lift(x: B): R
    finish(y: R): F21
end

object ReversedGeneralReduction[\B,R,F22\](r: GeneralReduction[\B,R,F22\]) extends GeneralReduction[\B,R,F22\]
  getter asString(): String = "ReversedGeneralReduction(" r.asString ")"
  getter reverse(): GeneralReduction[\B,R,F22\] = r
  getter id(): R = r.id
  join(x:R, y:R): R = r.join(y, x)
  lift(x: B): R = r.lift(x)
  finish(y: R): F22 = r.finish(y)
end



(** The usual lifting to Maybe for identity-less operators **)
(*) trait NonemptyReduction[\B\] extends GeneralReduction[\B, Maybe[\B\], B\]
(*)     getter id() = NothingObject[\B\]
(*)     join(a: Maybe[\B\], b: Maybe[\B\]): Maybe[\B\] =
(*)         if av <- a then
(*)             if bv <- b then
(*)                 Just(simpleJoin(av,bv))
(*)             else
(*)                 a
(*)             end
(*)         else
(*)             b
(*)         end
(*)     simpleJoin(a:Any, b:Any): Any
(*)     lift(r: B) = Just r
(*)     unlift(r: Maybe[\B\]): B =
(*)         if res <- r then
(*)             res
(*)         else
(*)             throw EmptyReduction
(*)         end
(*) end

trait CommutativeReduction[\R\] extends Reduction[\R\]
    getter reverse(): CommutativeReduction[\R\] = self
(*)     property FORALL(a: R, b: R) join(a,b) = join(b,a)
end

trait CommutativeGeneralReduction[\B,R,F23\]
      extends { CommutativeReduction[\R\], GeneralReduction[\B,R,F23\] }
    getter reverse(): CommutativeGeneralReduction[\B,R,F23\] = self
end

(** Monoids have lift and finish operation that are the identity function. **)
trait MonoidReduction[\R\] extends GeneralReduction[\R,R,R\]
    lift(r: R): R = r
    finish(r: R): R = r
end

(** A `MapReduceReduction` takes an associative binary function `j` on
    arguments of type `R`, and the identity of that function `id`, and
    returns the corresponding reduction. **)
object MapReduceReduction[\R\](j:(R,R)->R, id:R) extends MonoidReduction[\R\]
    getter asString(): String = "mapReduceReduction"
    join(a:R, b:R): R = (j)(a,b)
end

trait CommutativeMonoidReduction[\R\]
      extends { MonoidReduction[\R\], CommutativeGeneralReduction[\R,R,R\] }
end

trait ReductionWithZeroes[\R\] extends Reduction[\R\]
    isLeftZero(r: R): Boolean = isZero(r)
    isRightZero(r: R): Boolean = isZero(r)
    isZero(r: R): Boolean = false
end

trait GeneralReductionWithZeroes[\B,R,F24\]
      extends { ReductionWithZeroes[\R\],  GeneralReduction[\B,R,F24\] }
end

trait BigOperator[\I,B,R,F25,O\]
    getter body(): I->B
    getter reduction(): GeneralReduction[\B,R,F25\]
    getter unwrap(): F25->O
end

object BigReduction[\B,R,F26\](reduction:GeneralReduction[\B,R,F26\]) extends BigOperator[\B,B,R,F26,F26\]
    getter body(): B->B = fn x => x
    getter unwrap(): F26->F26 = fn x => x
end

object Comprehension[\I,B,R,F27,O\](body:I->B, reduction: GeneralReduction[\B,R,F27\], unwrap: F27->O)
        extends BigOperator[\I,B,R,F27,O\]
end

(** VoidReduction is usually done for effect, so we pretend that
    the completion performs the effects.  This rules out things
    distributing over void (that would change the number of effects in
    our program) but not void distributing over other things. **)
object VoidReduction extends { CommutativeMonoidReduction[\()\] }
    getter asString(): String = "VoidReduction"
    getter reverse() = VoidReduction
    getter id(): () = ()
    join(a: (), b: ()): () = ()
end

run(): () = println "PASS"

end


